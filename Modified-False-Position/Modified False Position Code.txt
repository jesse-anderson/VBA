Option Base 1
Option Explicit

Sub ModFalsePosition()

Dim xL As Double
Dim xR As Double
Dim fxL As Double
Dim fxR As Double
Dim xM As Double
Dim fxM As Double
Dim i As Integer
Dim iMax As Integer
Dim Tol As Double
Dim RepeatL As Integer
Dim RepeatR As Integer

'Make the current Worksheet the selected one:

Worksheets(ActiveSheet.Name).Activate

iMax = 100          'Maximum number of iterations
Tol = 0.000001      'Tolerance for zero

xL = Cells(2, 2)    'Choose that cell in matrix'
xR = Cells(2, 3)
fxL = fcalc(xL)
fxR = fcalc(xR)
Cells(2, 4) = fxL
Cells(2, 5) = fxR
If (fxL * fxR < 0) Then
  i = 1
  fxM = fxR
  While (i <= iMax And (Abs(fxL) > Tol) And (Abs(fxR) > Tol))
    i = i + 1
    xM = xL - fxL * (xR - xL) / (fxR - fxL)
    fxM = fcalc(xM)
    Cells(i, 6) = xM
    Cells(i, 7) = fxM
    If (fxR * fxM > 0) Then  'they have the same sign
      xR = xM
      fxR = fxM
      RepeatR = 0           'xR is not repeated
      RepeatL = RepeatL + 1 'xL is repeated
      If (RepeatL > 1) Then
        fxL = fxL / 2
      End If
    Else                   'fxM and fxL have the same sign
      xL = xM
      fxL = fxM
      RepeatL = 0           'xL is not repeated
      RepeatR = RepeatR + 1 'xR is repeated
      If (RepeatR > 1) Then
        fxR = fxR / 2 '
      End If
    End If
    Cells(i + 1, 1) = i
    Cells(i + 1, 2) = xL
    Cells(i + 1, 3) = xR
    Cells(i + 1, 4) = fxL
    Cells(i + 1, 5) = fxR
    Cells(i + 1, 8) = RepeatL
    Cells(i + 1, 9) = RepeatR
  Wend
  If (i >= iMax) Then
    Cells(i + 2, 1) = "Maximum iteration reached;"
    Cells(i + 3, 1) = "Solution might not be valid"
  Else
    Cells(i + 2, 1) = "Solution is within tolerance"
    Cells(i + 3, 1) = "x ="
    Cells(i + 3, 2) = xM
  End If
Else
    Cells(3, 1) = "Root is not bracketed, please try again"
End If
End Sub

Function fcalc(x)
  fcalc = Tan(x) - x - 0.5
End Function
